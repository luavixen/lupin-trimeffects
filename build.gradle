plugins {
    id 'dev.architectury.loom' version '1.9.424' apply false
    id 'architectury-plugin' version '3.4.160'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.mod_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "${rootProject.mod_name}-${project.name}-mc${rootProject.minecraft_version_min}"
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered {
            it.mappings("net.fabricmc:yarn:${rootProject.yarn_mappings}:v2")
            it.mappings("dev.architectury:yarn-mappings-patch-neoforge:${rootProject.yarn_mappings_patch_neoforge}")
        }
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    jar {
        manifest {
            attributes([
                'Specification-Title'     : rootProject.mod_name,
                'Specification-Vendor'    : 'luavixen',
                'Specification-Version'   : '1',
                'Implementation-Title'    : 'TrimsEffects',
                'Implementation-Version'  : rootProject.mod_version,
                'Implementation-Vendor'   : 'luavixen',
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            ])
        }
    }

    processResources {
        filesMatching(['fabric.mod.json', 'META-INF/mods.toml', 'META-INF/*.mods.toml']) {
            expand([
                'version': rootProject.mod_version,
                'minecraft_version': rootProject.minecraft_version,
                'minecraft_version_min': rootProject.minecraft_version_min,
                'minecraft_version_max': rootProject.minecraft_version_max,
                'minecraft_version_semver': rootProject.minecraft_version_semver,
            ])
        }
    }
}
