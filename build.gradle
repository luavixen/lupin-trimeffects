plugins {
    id 'dev.architectury.loom' version '1.6.394'
}

group = project.mod_group
archivesBaseName = project.mod_name

version = project.mod_version

loom {
    forge {
        mixinConfig 'trimeffects.mixins.json'
    }
    runs {
        data {
            data()
            programArgs '--existing', file('src/main/resources').absolutePath
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'Neoforged'
        url "https://maven.neoforged.net/releases/"
    }
}

configurations {
    includeImplementation

    implementation {
        extendsFrom includeImplementation
    }
    include {
        extendsFrom includeImplementation
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_version}:v2"
    forge "net.minecraftforge:forge:${project.forge_version}"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

compileJava {
    options.release = 17
    options.encoding = 'UTF-8'
}

processResources {
    filesMatching("META-INF/mods.toml") {
        expand 'version': version
    }
}

jar {
    manifest {
        attributes([
            'Specification-Title'     : project.mod_name,
            'Specification-Vendor'    : 'luavixen',
            'Specification-Version'   : '1',
            'Implementation-Title'    : 'Trim Effects',
            'Implementation-Version'  : project.mod_version,
            'Implementation-Vendor'   : 'luavixen',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
