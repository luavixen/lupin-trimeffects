plugins {
    id 'dev.architectury.loom' version '1.6.394'
}

group = project.mod_group
archivesBaseName = project.mod_name

version = project.mod_version

repositories {
    mavenCentral()
    maven {
        name = 'Neoforged'
        url "https://maven.neoforged.net/releases/"
    }
}

configurations {
    includeImplementation

    implementation {
        extendsFrom includeImplementation
    }
    include {
        extendsFrom includeImplementation
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered {
        it.mappings("net.fabricmc:yarn:1.20.5+build.1:v2")
        it.mappings("dev.architectury:yarn-mappings-patch-neoforge:1.20.5+build.3")
    }
    neoForge "net.neoforged:neoforge:${project.neoforge_version}"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

compileJava {
    options.release = 21
    options.encoding = 'UTF-8'
}

processResources {
    filesMatching("META-INF/neoforge.mods.toml") {
        expand 'version': version
    }
}

jar {
    manifest {
        attributes([
            'Specification-Title'     : project.mod_name,
            'Specification-Vendor'    : 'luavixen',
            'Specification-Version'   : '1',
            'Implementation-Title'    : 'Trim Effects',
            'Implementation-Version'  : project.mod_version,
            'Implementation-Vendor'   : 'luavixen',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
